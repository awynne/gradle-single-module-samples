apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'project-report'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'pmd'
apply plugin: 'build-dashboard'

group = 'org.minazou67'
version = '1.0.0-SNAPSHOT'

description = 'This is a sample of a single module of the Gradle project.'

def jdkVersion = 1.7
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava, javadoc].each {
    it.options.encoding = defaultEncoding
}

configurations {
    javancssConf
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group:'org.slf4j', name:'slf4j-api', version:'1.7.7'
    compile group:'org.slf4j', name:'slf4j-nop', version:'1.7.7'
    compile group:'org.apache.commons', name:'commons-lang3', version:'3.3.2'
    compile group:'org.apache.commons', name:'commons-collections4', version:'4.0'
    testCompile group:'junit', name:'junit', version:'4.11'
    testCompile group:'org.mockito', name:'mockito-all', version:'1.9.5'
    javancssConf group:'org.codehaus.javancss', name:'javancss', version:'33.54'
}

javadoc {
    options {
        charSet = defaultEncoding
        docEncoding = defaultEncoding
    }
}

jacoco {
    toolVersion = "0.7.2.201409121644"
}

jacocoTestReport {
    description = "Generate Jacoco coverage reports after running tests."
    dependsOn 'test'
    reports {
        csv.enabled false
        xml.enabled true
        html.enabled true
    }
}
check.dependsOn(jacocoTestReport)

checkstyle {
    toolVersion = '6.0'
    showViolations = true
    configFile = file("$rootDir/checkstyle.xml")
    checkstyleTest.enabled = false
}

findbugs {
    toolVersion = '3.0.0'
    effort = "max"
    ignoreFailures = true
    findbugsTest.enabled = false
}

jdepend {
    toolVersion = '2.9.1'
    ignoreFailures = true
    jdependTest.enabled = false
}

pmd {
    toolVersion = '5.1.3'
    ignoreFailures = true
    targetJdk = jdkVersion
    pmdTest.enabled = false
}

task sourcesJar(type: Jar) {
    description = 'Assembles a jar archive containing the main sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Assembles a jar archive containing the javadoc.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task javancss() {
    description = 'Run JavaNCSS analysis for main classes.'
    def javancssDir = file("$project.buildDir/reports/javancss")
    javancssDir.mkdirs()
    ant {
        taskdef(name:'javancss', classname:'javancss.JavancssAntTask', classpath:configurations.javancssConf.asPath)
        javancss(srcdir:'src/main/java', generateReport:'true', outputfile:"$javancssDir/javancss-metrics.txt", format:'plain', includes:'**/*.java')
    }
}
check.dependsOn(javancss)

task wrapper(type: Wrapper) {
    description = 'Download Gradle and generate wrapper scripts.'
    gradleVersion = '2.1'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

[jar, sourcesJar, javadocJar].each {
    it.from 'LICENSE'
    it.rename '(LICENSE)', 'META-INF/$1'
    it.manifest {
        attributes(
            "Manifest-Version": 1.0,
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor-Id": project.group,
            "Build-Jdk": System.properties['java.version'],
            "Created-By": "Gradle " + gradle.gradleVersion,
        )
    }
}
